apply from: file('gradle/project.gradle')

subprojects {
    apply from: file("${rootDir}/gradle/check.gradle")

    group = 'core.aws'
    version = '1.9.0'
}

def awsVersion = '1.11.313'
def junitVersion = '5.1.0'

project(':cmn') {
    apply plugin: 'application'
    mainClassName = 'Main'

    startScripts {
        defaultJvmOpts = ['-Dcmn.ansible=APP_HOME_VAR/ansible']

        doLast {
            windowsScript.text = windowsScript.text.replaceFirst('APP_HOME_VAR', '%APP_HOME%')
            unixScript.text = unixScript.text.replaceFirst('APP_HOME_VAR', '\\$APP_HOME')
        }
    }

    dependencies {
        compile "com.amazonaws:aws-java-sdk-autoscaling:${awsVersion}"
        compile "com.amazonaws:aws-java-sdk-ec2:${awsVersion}"
        compile "com.amazonaws:aws-java-sdk-elasticloadbalancing:${awsVersion}"
        compile "com.amazonaws:aws-java-sdk-elasticloadbalancingv2:${awsVersion}"
        compile "com.amazonaws:aws-java-sdk-cloudwatch:${awsVersion}"
        compile "com.amazonaws:aws-java-sdk-iam:${awsVersion}"
        compile "com.amazonaws:aws-java-sdk-s3:${awsVersion}"
        compile "com.amazonaws:aws-java-sdk-ecr:${awsVersion}"
        compile 'org.slf4j:slf4j-api:1.7.25'
        compile 'org.slf4j:jcl-over-slf4j:1.7.25'
        compile 'ch.qos.logback:logback-classic:1.2.3'
        compile 'com.jcraft:jsch:0.1.54'
        compile 'org.apache.commons:commons-compress:1.16.1'
        compile 'org.yaml:snakeyaml:1.19'

        testCompile "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testCompile 'org.mockito:mockito-core:2.16.0'
        testCompile 'org.assertj:assertj-core:3.9.0'

        testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    }
}
